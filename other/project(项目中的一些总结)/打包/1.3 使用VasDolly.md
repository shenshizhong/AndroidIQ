
VasDolly打包（多渠道）
```
1、
app存放要打的渠道信息文件:channel.txt

2、
gradle.properties 写入：
android.useAndroidX=true
channel_file=channel.txt

3、
项目的build.gradle:
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.1.2"
        classpath 'com.tencent.vasdolly:plugin:3.0.4'
    }
}

4、
app中的build.gradle:

plugins {
    id 'com.android.application'
    id 'com.tencent.vasdolly'
}

android {
    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword "123456"
            keyAlias "testAAR"
            keyPassword "123456"
            enableV1Signing true
            enableV2Signing true
        }

        debug {
            storeFile file("release.keystore")
            storePassword "123456"
            keyAlias "testAAR"
            keyPassword "123456"
            enableV1Signing true
            enableV2Signing false
        }
    }

   buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
 implementation "com.tencent.vasdolly:helper:3.0.4"
}



//VasDolly插件配置
//gradle channelDebug/channelRelease 编译生成apk后，再根据生成的Apk生成渠道包
channel{
    channelFile = file("app_channel.txt")
    //多渠道包的输出目录，默认为new File(project.buildDir,"channel")
    outputDir = new File(project.buildDir,"channels")
    //多渠道包的命名规则，默认为：${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}-${buildTime}
    apkNameFormat ='${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}'
    //快速模式：生成渠道包时不进行校验（速度可以提升10倍以上，默认为false）
    fastMode = false
    //buildTime的时间格式，默认格式：yyyyMMdd-HHmmss
    buildTimeDateFormat = 'yyyyMMdd-HH:mm:ss'
    //低内存模式（仅针对V2签名，默认为false）：只把签名块、中央目录和EOCD读取到内存，不把最大头的内容块读取到内存，在手机上合成APK时，可以使用该模式
    lowMemory = false
}

rebuildChannel {
    channelFile = file("app_channel.txt")
    //指定的apk生成渠道包,文件名中如果有base将被替换为渠道名，否则渠道名将作为前缀
    baseApk = new File(project.buildDir, "outputs/apk/debug/app-debug.apk")
    //默认为new File(project.buildDir, "rebuildChannel")
    outputDir = new File(project.buildDir, "rebuildChannels")
    //快速模式：生成渠道包时不进行校验（速度可以提升10倍以上，默认为false）
    fastMode = false
    //低内存模式（仅针对V2签名，默认为false）：只把签名块、中央目录和EOCD读取到内存，不把最大头的内容块读取到内存，在手机上合成APK时，可以使用该模式
    lowMemory = false
}



```